macro(reason_set_or KEY VALUE DEFAULT)
  if(VALUE)
    set("${KEY}" "${VALUE}")
  else()
    set("${KEY}" "${DEFAULT}")
  endif()
endmacro()

macro(reason_set_if KEY VALUE)
  if(NOT "${VALUE}" STREQUAL "")
    set("${KEY}" "${VALUE}")
  endif()
endmacro()

macro(reason_set_check KEY ERRMSG)
  if(NOT ${KEY})
    reason_message(FATAL_ERROR "${ERRMSG}")
  endif()
endmacro()

function(reason_message)
  reason_define_colors()
  list(GET ARGV 0 MessageType)
  if(MessageType STREQUAL FATAL_ERROR OR MessageType STREQUAL SEND_ERROR)
    list(REMOVE_AT ARGV 0)
    message("${MessageType}" "${c_bred}${ARGV}${c_reset}")
  elseif(MessageType STREQUAL WARNING)
    list(REMOVE_AT ARGV 0)
    message("${MessageType}" "${c_byellow}${ARGV}${c_reset}")
  elseif(MessageType STREQUAL AUTHOR_WARNING)
    list(REMOVE_AT ARGV 0)
    message("${MessageType}" "${c_baqua}${ARGV}${c_reset}")
  elseif(MessageType STREQUAL STATUS)
    list(REMOVE_AT ARGV 0)
    message("${MessageType}" "${c_green}${ARGV}${c_reset}")
  elseif(MessageType STREQUAL VERBOSE)
    list(REMOVE_AT ARGV 0)
    message("${c_bfuchsia}${ARGV}${c_reset}")
  else()
    message("${c_silver}${ARGV}${c_reset}")
  endif()
endfunction()

function(reason_verbose)
  if(REASON_VERBOSE)
    if(REASON_BRIEF_PATH)
      string(REPLACE "${CMAKE_SOURCE_DIR}" "~" ARGV "${ARGV}")
    endif()
    reason_message(VERBOSE "${ARGV}")
  endif()
endfunction()

function(reason_print_help)
  reason_message(AUTHOR_WARNING "${ARGV}")
  reason_message(FATAL_ERROR)
endfunction()

function(reason_util_configure_and_include IN_FILE OUT_FILE)
  set(OUT_FILE_FULL "${CMAKE_BINARY_DIR}/reason.cmake/${OUT_FILE}")
  if(EXISTS OUT_FILE_FULL)
    include("${OUT_FILE_FULL}")
  else()
    configure_file("${REASON_MODULE_DIR}/${IN_FILE}" "${OUT_FILE_FULL}" @ONLY)
    include("${OUT_FILE_FULL}")
  endif()
endfunction()

function(reason_extract_dep_inc_dirs_to_target TARGET_NAME DEP)
  reason_verbose("  add dep include directories: [dep=${DEP}]")
  get_target_property(DEP_INC_DIRS "${DEP}" "INCLUDE_DIRECTORIES")
  if("${DEP_INC_DIRS}" STREQUAL "DEP_INC_DIRS-NOTFOUND")
    return()
  endif()
  foreach(v IN LISTS DEP_INC_DIRS)
    target_include_directories("${TARGET_NAME}" PRIVATE "${v}")
    reason_verbose("    [private-include=${v}]")
  endforeach()
endfunction()

function(reason_unique_target_properties TARGET_NAME)
  # @TODO compile define
  get_target_property(t_INCLUDE_DIRS "${TARGET_NAME}" "INCLUDE_DIRECTORIES")
  if(NOT "${t_INCLUDE_DIRS}" STREQUAL "t_INCLUDE_DIRS-NOTFOUND")
    list(REMOVE_DUPLICATES t_INCLUDE_DIRS)
    set_target_properties("${TARGET_NAME}" PROPERTIES "INCLUDE_DIRECTORIES" "${t_INCLUDE_DIRS}")
  endif()

  get_target_property(t_LINK_LIBS "${TARGET_NAME}" "LINK_LIBRARIES")
  if(NOT "${t_LINK_LIBS}" STREQUAL "t_LINK_LIBS-NOTFOUND")
    list(REMOVE_DUPLICATES t_LINK_LIBS)
    set_target_properties("${TARGET_NAME}" PROPERTIES "LINK_LIBRARIES" "${t_LINK_LIBS}")
  endif()
endfunction()

function(reason_print_target_properties TARGET_NAME)
  if(NOT REASON_VERBOSE)
    return()
  endif()

  function(reason_print_target_properties_one TARGET_NAME PROP_NAME)
    get_target_property(props "${TARGET_NAME}" "${PROP_NAME}")
    if("${props}" STREQUAL "props-NOTFOUND")
      reason_verbose("  ${PROP_NAME}: None")
    else()
      reason_verbose("  ${PROP_NAME}:")
      foreach(prop IN LISTS props)
        reason_verbose("    ${prop}")
      endforeach()
    endif()
  endfunction()

  reason_verbose("${TARGET_NAME} properties:")

  # @TODO compile define
  reason_print_target_properties_one("${TARGET_NAME}" "INTERFACE_COMPILE_OPTIONS")
  reason_print_target_properties_one("${TARGET_NAME}" "INTERFACE_INCLUDE_DIRECTORIES")
  reason_print_target_properties_one("${TARGET_NAME}" "INTERFACE_LINK_LIBRARIES")

  reason_print_target_properties_one("${TARGET_NAME}" "COMPILE_OPTIONS")
  reason_print_target_properties_one("${TARGET_NAME}" "INCLUDE_DIRECTORIES")
  reason_print_target_properties_one("${TARGET_NAME}" "LINK_LIBRARIES")
endfunction()
